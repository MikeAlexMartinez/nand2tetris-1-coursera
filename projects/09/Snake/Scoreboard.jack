
/**
 * Controls the display of the scoreboard
 * height: 100%
 * width: 16 * 8px = 128px or 8 bytes
 */
class Scoreboard {

  field int score; // tracks user score
  field int speed; // tracks game speed

  constructor Scoreboard new(int gameSpeed) {
    let score = 0;

    if (gameSpeed = 0) {
      let speed = 1;
    } else {
      let speed = gameSpeed;
    }

    do setHeading();
    do setScore(0);
    do setSpeed();

    return this;
  }

  method void dispose() {
    do Memory.deAlloc(this);
    return;
  }

  /* Adds the heading SNAKE to the scoreboard */
  method void setHeading() {
    do Output.moveCursor(1, 1);
    do Output.printString("SNAKE");
    return;
  }

  /* Add current score */
  method void setScore(int startScore) {
    let score = startScore;
    do Output.moveCursor(3, 1);
    do Output.printString("CURRENT SCORE:");
    do updateScore(startScore);
    return;
  }

  /* Updates the current score of the user */
  method void updateScore(int points) {
    let score = score + points;
    do Output.moveCursor(4, 1);
    do Output.printInt(score);
    do Output.printString("     ");
    return;
  }

  /* Sets start speed of the game: defaults to 10 */
  method void setSpeed() {
    do Output.moveCursor(7, 1);
    do Output.printString("SPEED: ");
    do updateSpeed(speed);
    return;
  }

  /* Update speed of the game on the scoreboard */
  method void updateSpeed(int newSpeed) {
    let speed = newSpeed;
    do Output.moveCursor(7, 8);
    do Output.printInt(newSpeed);
    do Output.printString(" ");
    return;
  }

  method int getSpeed() {
    return speed;
  }

  /* show direction */
  method void updateDirection(int direction) {
    do Output.moveCursor(12, 1);
    do Output.printString("Direction: ");
    do Output.printInt(direction);
    return;
  }

  /* show message */
  method void showMessage(String message) {
    do Output.moveCursor(14, 1);
    do Output.printString(message);
    return;
  }

  /* clear message */
  method void clearMessage() {
    do Output.moveCursor(14, 1);
    do Output.printString("               ");
    return;
  }

  /* Game over! */
  method void gameOver() {
    do Output.moveCursor(14, 1);
    do Output.printString("GAME OVER!");
    return;
  }
}
