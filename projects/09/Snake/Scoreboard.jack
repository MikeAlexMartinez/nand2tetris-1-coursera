
/**
 * Controls the display of the scoreboard
 * height: 100%
 * width: 16 * 8px = 128px or 8 bytes
 */
class Scoreboard {

  field int score; // tracks user score

  constructor Scoreboard new() {
    let score = 0;

    do setHeading();
    do setScore();
    do setSpeed();

    return this;
  }

  method void dispose() {
    do Memory.deAlloc(this);
    return;
  }

  /* Adds the heading SNAKE to the scoreboard */
  method void setHeading() {
    do Output.moveCursor(1, 1);
    do Output.printString("SNAKE");
    return;
  }

  /* Add current score */
  method void setScore() {
    do Output.moveCursor(3, 1);
    do Output.printString("CURRENT SCORE:");
    do updateScore(0);
    return;
  }

  /* Updates the current score of the user */
  method void updateScore(int points) {
    let score = score + points;
    do Output.moveCursor(4, 1);
    do Output.printInt(score);
    return;
  }

  /* Sets start speed of the game: defaults to 10 */
  method void setSpeed() {
    do Output.moveCursor(7, 1);
    do Output.printString("SPEED: ");
    do updateSpeed(10);
    return;
  }

  /* Update speed of the game on the scoreboard */
  method void updateSpeed(int newSpeed) {
    do Output.moveCursor(7, 8);
    do Output.printInt(newSpeed);
    return;
  }

  /* Display Game State */
  method void showState(int state) {
    do Output.moveCursor(10, 1);
    do Output.printInt(state);
    return;
  }

  /* show direction */
  method void updateDirection(int direction) {
    do Output.moveCursor(12, 1);
    do Output.printString("Direction: ");
    do Output.printInt(direction);
    return;
  }
}
